apply plugin: 'com.android.library'

android {
    compileSdkVersion 25

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

}

// 定义lintJarImport方法，在copyLintJar任务中被调用
configurations {
    lintJarImport
}

dependencies {
    // 调用lintjar的lintJarOutput方法，获得jar包
    lintJarImport project(path: ':lintjar', configuration: 'lintJarOutput')
}

// 调用lintJarImport得到jar包，拷贝到指定目录
task copyLintJar(type: Copy) {
    from (configurations.lintJarImport) {
        rename {
            String fileName ->
                'lint.jar'
        }
    }
    into 'build/intermediates/lint/'
}

// 当项目执行到prepareLintJar这一步时执行copyLintJar方法（注意：这个时机需要根据项目具体情况改变）
project.afterEvaluate {
    def compileLintTask = project.tasks.find{ it.name == 'prepareLintJar'}
    compileLintTask.dependsOn(copyLintJar)
}
